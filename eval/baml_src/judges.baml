class Service {
  name string
  priceBRL float
}

class BusinessProfile {
  businessName string
  businessType string
  city string
  neighbourhood string
  services Service[]
  targetAudience string
  brandVibe string
  quirks string[]
}

class ContentRole {
  name string
  description string
}

class Post {
  caption string
  hashtags string[]
  productionNote string
}

class JudgeResult {
  reasoning string
  verdict bool
}

class JudgeVariedadeResult {
  postMessages string[]
  reasoning string
  verdict bool
}

function JudgeNaturalidade(profile: BusinessProfile, content: string) -> JudgeResult {
  client JudgeClient
  prompt #"
    VocÃª Ã© um avaliador rigoroso de conteÃºdo para Instagram brasileiro.

    JÃ¡ foi verificado que o texto usa portuguÃªs brasileiro informal. Sua tarefa Ã© diferente: avaliar se o texto parece escrito por uma PESSOA REAL ou por uma IA imitando o estilo do Instagram.

    Perfil do negÃ³cio:
    - Nome: {{ profile.businessName }}
    - Tipo: {{ profile.businessType }}
    - Cidade: {{ profile.city }}
    - Bairro: {{ profile.neighbourhood }}

    ConteÃºdo a avaliar:
    ---
    {{ content }}
    ---

    Sinais de conteÃºdo gerado por IA (reprove se encontrar 2 ou mais):
    - Emoji em quase toda frase, como decoraÃ§Ã£o automÃ¡tica
    - Mesma estrutura nos posts: abertura animada â†’ informaÃ§Ã£o â†’ pergunta â†’ CTA
    - Informalidade forÃ§ada: acumula gente, bora, nÃ©, tÃ¡ no mesmo parÃ¡grafo como checklist
    - Frases genÃ©ricas de preenchimento ("feito com muito carinho", "vocÃª merece o melhor", "a gente ama o que faz")
    - Tom uniformemente entusiasmado do inÃ­cio ao fim, sem variaÃ§Ã£o de energia

    Sinais de conteÃºdo autÃªntico (aprove se predominarem):
    - Voz com personalidade prÃ³pria, nÃ£o "brasileiro genÃ©rico de Instagram"
    - Ritmo variado: mistura frases curtas e longas naturalmente
    - Emojis com intenÃ§Ã£o, nÃ£o em toda frase
    - Pelo menos um momento que soa como opiniÃ£o pessoal, nÃ£o fÃ³rmula

    Exemplo de reprovaÃ§Ã£o (deve receber verdict: false):
    "Gente, vocÃªs nÃ£o tÃ£o prontos! ğŸ˜ğŸ”¥ Nosso smash Ã© feito com muito amor e dedicaÃ§Ã£o pra vocÃªs! A gente ama o que faz e isso faz toda a diferenÃ§a, nÃ©? ğŸ’• Cada detalhe Ã© pensado com carinho pra vocÃªs! Bora experimentar? Chama no WhatsApp! ğŸ˜˜"
    Motivo: emoji em toda frase, "feito com amor e dedicaÃ§Ã£o" + "pensado com carinho" (filler genÃ©rico), gente + nÃ© + bora empilhados no mesmo parÃ¡grafo, tom 100% entusiasmado sem pausa. Parece IA performando informalidade.

    Primeiro explique seu raciocÃ­nio em 2-3 frases, depois dÃª o veredito.
    Veredito: true se soa autÃªntico, false se parece gerado por IA.

    {{ ctx.output_format }}
  "#
}

function JudgeEspecificidade(profile: BusinessProfile, content: string) -> JudgeResult {
  client JudgeClient
  prompt #"
    VocÃª Ã© um avaliador rigoroso de conteÃºdo para Instagram brasileiro.

    Sua tarefa: o conteÃºdo tem detalhes que existem POR SI SÃ“S, ou todo detalhe inventado serve apenas para vender o produto/serviÃ§o?

    Perfil do negÃ³cio (dados que a IA recebeu):
    - Nome: {{ profile.businessName }}
    - Tipo: {{ profile.businessType }}
    - Cidade: {{ profile.city }}
    - Bairro: {{ profile.neighbourhood }}
    - ServiÃ§os: {% for s in profile.services %}{{ s.name }} (R${{ s.priceBRL }}){% if not loop.last %}, {% endif %}{% endfor %}
    - PÃºblico: {{ profile.targetAudience }}
    - Vibe: {{ profile.brandVibe }}
    - Diferenciais: {% for q in profile.quirks %}{{ q }}{% if not loop.last %}, {% endif %}{% endfor %}

    ConteÃºdo a avaliar:
    ---
    {{ content }}
    ---

    Teste decisivo: para cada detalhe inventado, tire a menÃ§Ã£o ao produto/serviÃ§o. O detalhe ainda tem valor para o leitor? Se nÃ£o, Ã© decoraÃ§Ã£o de pitch.

    EXEMPLO 1 â€” verdict: false (dados do perfil reformatados)
    "Aqui no Setor Bueno a gente faz smash burger com nosso blend secreto ğŸ” O molho da casa Ã© preparado todo dia! Simples por R$28, duplo por R$38, combo completo por R$52. Bora provar?"
    Motivo: Setor Bueno = campo bairro, blend secreto = campo diferenciais, preÃ§os = campo serviÃ§os. Cada informaÃ§Ã£o veio do perfil. Zero textura.

    EXEMPLO 2 â€” verdict: false (pitch embrulhado em histÃ³ria)
    "Era uma terÃ§a Ã  noite e a Maria, dona de uma loja de roupas, tava exausta tentando escrever uma legenda pro Instagram. Ela nÃ£o sabia o que postar. Foi aÃ­ que ela descobriu o AppX. O AppX olha pro conteÃºdo dela e escreve a legenda perfeita. Maria nunca mais travou."
    Motivo: tire o AppX e a histÃ³ria da Maria nÃ£o tem razÃ£o de existir. A cena foi inventada apenas para montar o pitch. Isso nÃ£o Ã© especificidade, Ã© narrativa instrumental.

    EXEMPLO 3 â€” verdict: true (detalhes com vida prÃ³pria)
    "Sexta 18h e o cheiro da chapa jÃ¡ tÃ¡ chamando a galera aqui no Bueno ğŸ”¥ Tem fila? Tem. Mas quem jÃ¡ mordeu o duplo sabe que vale cada minuto. Hoje o Rafa tÃ¡ no comando da chapa, capricho dobrado ğŸ˜‚"
    Motivo: "sexta 18h" (cena temporal), "cheiro da chapa" (sensorial), "tem fila" (observaÃ§Ã£o), "Rafa no comando" (personagem). Tire o produto e a cena ainda pinta um momento real. Os detalhes enriquecem por si sÃ³s.

    Primeiro explique seu raciocÃ­nio em 2-3 frases, depois dÃª o veredito.
    Veredito: true se os detalhes inventados valem por si sÃ³s, false se servem apenas ao pitch.

    {{ ctx.output_format }}
  "#
}

function JudgeAcionavel(profile: BusinessProfile, content: string) -> JudgeResult {
  client JudgeClient
  prompt #"
    VocÃª Ã© um avaliador rigoroso de conteÃºdo para Instagram brasileiro.

    JÃ¡ foi verificado que o texto tem legenda, hashtags, CTA e nota de produÃ§Ã£o. Sua tarefa Ã© diferente: avaliar a QUALIDADE desses elementos.

    Perfil do negÃ³cio:
    - Nome: {{ profile.businessName }}
    - Tipo: {{ profile.businessType }}

    ConteÃºdo a avaliar:
    ---
    {{ content }}
    ---

    Avalie estes 4 critÃ©rios de qualidade:

    HASHTAGS: reprove se a maioria for genÃ©rica (#sucesso #empreendedorismo #foco #motivaÃ§Ã£o #trabalho #pequenonegocio #apoielocal). Aprove se forem especÃ­ficas do nicho, do bairro ou do negÃ³cio.

    NOTA DE PRODUÃ‡ÃƒO: reprove se for vaga ("tire uma foto do produto", "grave um vÃ­deo mostrando o serviÃ§o"). Aprove se disser o que filmar, de que Ã¢ngulo, em que momento.

    CTA: reprove se for o mesmo CTA copiado em todos os posts. Aprove se variar entre posts.

    FLUIDEZ: reprove se a legenda parecer seÃ§Ãµes coladas (texto â†’ bloco de hashtags â†’ CTA solto â†’ nota solta). Aprove se a transiÃ§Ã£o entre elementos for natural.

    Reprove se 2 ou mais critÃ©rios falharem.

    Exemplo de reprovaÃ§Ã£o (elementos existem mas sem qualidade):
    Post 1: "... Chama no WhatsApp! #empreendedorismo #sucesso #foco #negociolocal #motivaÃ§Ã£o. Nota de produÃ§Ã£o: tire uma foto bonita do produto."
    Post 2: "... Chama no WhatsApp! #empreendedorismo #sucesso #negociolocal #trabalho #foco. Nota de produÃ§Ã£o: grave um vÃ­deo do serviÃ§o."
    Motivo: hashtags genÃ©ricas repetidas, mesmo CTA em ambos, notas de produÃ§Ã£o vagas. Tudo existe estruturalmente mas nada tem qualidade.

    Primeiro explique seu raciocÃ­nio em 2-3 frases, depois dÃª o veredito.
    Veredito: true se os elementos tÃªm qualidade, false se sÃ£o genÃ©ricos/repetitivos.

    {{ ctx.output_format }}
  "#
}

function JudgeVariedade(profile: BusinessProfile, content: string) -> JudgeVariedadeResult {
  client JudgeClient
  prompt #"
    VocÃª Ã© um avaliador rigoroso de conteÃºdo para Instagram brasileiro.

    Perfil do negÃ³cio:
    - Nome: {{ profile.businessName }}
    - Tipo: {{ profile.businessType }}

    ConteÃºdo a avaliar:
    ---
    {{ content }}
    ---

    TAREFA em 2 passos:

    PASSO 1: Para cada post, escreva em UMA frase curta o que o leitor leva depois de ler. Coloque cada frase no campo postMessages. ATENÃ‡ÃƒO: se todas as frases mencionam o mesmo produto/serviÃ§o como soluÃ§Ã£o, elas sÃ£o a mesma mensagem. Escreva sem mencionar o nome do produto.

    PASSO 2: Compare as frases. Se sÃ£o essencialmente a mesma ("use X", "experimente X", "X resolve"), reprove.

    Exemplo que REPROVA (verdict: false):
    Post 1 (histÃ³ria): "Era terÃ§a Ã  noite e eu vi minha amiga Ana travada tentando escrever uma legenda. O AppX nasceu ali. Testa, o link tÃ¡ na bio."
    Post 2 (nÃºmeros): "1.500 pessoas jÃ¡ baixaram o AppX. O pequeno negÃ³cio quer mostrar o trabalho sem gastar horas num post."
    Post 3 (citaÃ§Ã£o): "Um dono de oficina me disse que Instagram virou trabalho nÃ£o remunerado. O AppX resolve isso."
    postMessages: ["Existe soluÃ§Ã£o pra quem trava na hora de postar", "Existe soluÃ§Ã£o pra quem trava na hora de postar", "Existe soluÃ§Ã£o pra quem trava na hora de postar"]
    Motivo: sem o nome do produto, as trÃªs mensagens sÃ£o idÃªnticas. TrÃªs estruturas, um sÃ³ pitch.

    Exemplo que APROVA (verdict: true):
    Post 1: "Sexta 18h e o cheiro da chapa jÃ¡ tÃ¡ chamando a galera ğŸ”¥ Tem fila? Tem. Mas quem jÃ¡ mordeu o duplo sabe que vale cada minuto."
    Post 2: "3 erros que todo mundo comete na hora de montar o hambÃºrguer em casa: carne fria na chapa, pÃ£o sem tostar, queijo errado."
    Post 3: "Pergunta honesta: alguÃ©m consegue comer smash sem fazer sujeira? Porque aqui a gente jÃ¡ desistiu ğŸ˜‚"
    postMessages: ["Vale esperar na fila", "Como fazer melhor em casa", "HambÃºrguer Ã© pra curtir sem frescura"]
    Motivo: cada post dÃ¡ ao leitor algo diferente para pensar.

    Se houver apenas um post, coloque sua mensagem em postMessages e avalie se demonstra criatividade.

    {{ ctx.output_format }}
  "#
}

function JudgeEngajamento(profile: BusinessProfile, content: string) -> JudgeResult {
  client JudgeClient
  prompt #"
    VocÃª Ã© um avaliador rigoroso de conteÃºdo para Instagram brasileiro.

    JÃ¡ foi verificado que o texto tem CTA e convite Ã  interaÃ§Ã£o. Sua tarefa Ã© diferente: avaliar se o gancho Ã© GENUINAMENTE interessante ou apenas uma fÃ³rmula de engajamento.

    Perfil do negÃ³cio:
    - Nome: {{ profile.businessName }}
    - Tipo: {{ profile.businessType }}
    - PÃºblico: {{ profile.targetAudience }}

    ConteÃºdo a avaliar:
    ---
    {{ content }}
    ---

    Reprove se:
    - O gancho usa fÃ³rmulas batidas: "VocÃª sabia que...?", "Gente, prepara o coraÃ§Ã£o!", "VocÃªs nÃ£o estÃ£o prontos!", "[NÃºmero] coisas que..."
    - O engajamento depende de pedir aÃ§Ã£o genÃ©rica ("comenta aqui ğŸ‘‡", "marca um amigo") sem dar motivo real para fazÃª-lo
    - O conteÃºdo descreve o serviÃ§o de forma positiva mas sem Ã¢ngulo surpreendente
    - Qualquer negÃ³cio do mesmo tipo poderia usar o mesmo gancho

    Aprove se:
    - A primeira linha cria curiosidade real (um dado especÃ­fico, uma cena, uma contradiÃ§Ã£o, uma histÃ³ria que comeÃ§a no meio)
    - HÃ¡ motivo real pra salvar, compartilhar ou comentar (aprendi algo novo, me identifiquei com a situaÃ§Ã£o, quero mandar pra alguÃ©m especÃ­fico)
    - O gancho sÃ³ funciona para este negÃ³cio, nÃ£o Ã© genÃ©rico do nicho

    Exemplo de reprovaÃ§Ã£o (fÃ³rmula de engajamento):
    "VocÃª sabia que um bom corte pode mudar completamente seu visual? ğŸ˜± Pois Ã©! Aqui no nosso espaÃ§o a gente transforma! Antes e depois que vai te deixar de queixo caÃ­do! Comenta aqui se vocÃª tambÃ©m ama! ğŸ‘‡ Marca aquele amigo que tÃ¡ precisando! ğŸ˜‚"
    Motivo: "VocÃª sabia" (gancho genÃ©rico), "mudar completamente seu visual" (Ã³bvio, qualquer salÃ£o diria isso), "comenta + marca" sem dar motivo real. FÃ³rmula, nÃ£o engajamento.

    Primeiro explique seu raciocÃ­nio em 2-3 frases, depois dÃª o veredito.
    Veredito: true se o engajamento Ã© genuÃ­no, false se Ã© fÃ³rmula.

    {{ ctx.output_format }}
  "#
}
